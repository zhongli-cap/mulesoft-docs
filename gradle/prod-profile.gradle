// Command to execute in CI:
// ./gradlew -g .gradle --no-search-upward -Pprofile=prod -PbuildTag=$BUILD_TAG -PmaxHeapSize=4g clean publish
ext {
  assetsRepoBranchName = 'prod'
  assetsRepoCommit = '36f0a3126b07ed5b37a00f83aa8e4d726c9dfbef'
  publishCommitMessage = project.hasProperty('buildTag') ? project.property('buildTag') : 'publish changes'
  publishRepoBranchName = 'master'
  publishRepoUri = 'git@github.com:mulesoft/mulesoft-docs-build-output.git'
  siteDir = '_publish'
  siteUri = 'https://docs.mulesoft.com'
}

task clonePublishRepo {
  outputs.dir(siteDir).upToDateWhen { false }
  doLast {
    if (file("$siteDir/.git").directory) {
      git "fetch -q origin $publishRepoBranchName", siteDir
      git "reset -q --hard origin/$publishRepoBranchName", siteDir
    }
    else {
      git "clone -q --single-branch --depth 1 $publishRepoUri $siteDir", file(siteDir).parent
    }
  }
}

task prepareSiteDir {
  inputs.dir siteDir
  doLast {
    ant.delete(includeemptydirs: true) {
      fileset(dir: siteDir, defaultexcludes: false) {
        exclude(name: '.git/**')
      }
    }
  }
}

task publishSiteDir {
  onlyIf { !git('status --porcelain', siteDir, true).empty }
  doLast {
    git 'add .', siteDir
    git "commit -q -m \"$publishCommitMessage\"", siteDir
    if (!dryRun) git "push -q origin $publishRepoBranchName", siteDir
  }
}

task publish(group: 'Build', description: 'Builds site and commits changes to publisher repository.')

afterEvaluate {
  clean.delete = assetsDir // don't delete _publish directory
  //copyAssets.outputs.upToDateWhen { false }
  prepareSiteDir.dependsOn clonePublishRepo
  publish.dependsOn prepareSiteDir, build, publishSiteDir
  build.mustRunAfter prepareSiteDir
  publishSiteDir.mustRunAfter build
}
