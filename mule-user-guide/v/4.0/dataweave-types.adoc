= DataWeave Value Types
:keywords: studio, anypoint, esb, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

image:dw-logo.png[dw]

The link:/mule-user-guide/v/4.0/dataweave[DataWeave] language functions essentially as a template engine that describes an output structure through the use of elements that may be of various different types.

This document covers the different types of objects you can use to construct your output.





== Array
*Type* => 'Array'

Arrays are represented as a sequences of *value* expressions.

[source,DataWeave]
--------------------------------------------------------------------
[ 1, 2 + 2, 3 * 3, $x ]
--------------------------------------------------------------------

*Array Literal*
[source,DataWeave,linenums]
----
%dw 2.0
%output application/json
---
[ "My", "three", "words" ]
----

=== Conditional Elements

Arrays can define conditional *values* based on a condition.
Wrap the value expression between parentheses and use the *when* keyword with the condition.

.Transform
[source,DataWeave,linenums]
----
%dw 2.0
%output application/json
---
[(1) when true, (2) when false]
----

.Output
[source,json,linenums]
----
[1]
----

== Object

*Type* => 'Object'

Objects are represented as a collection of *key*: *value* pairs.

. Object: { 'Key' : Value }
. Key : 'Qualified Name' 'Attributes'
. Qualified Name: 'namespace prefix#name' where the 'namespace prefix#' part is optional
. Name: String that represents the name.
. Attributes: @('Qualified Name'= *value*,...)

[TIP]
Strings must be double quoted to be recognized as strings.

=== Single Value Objects

If an Object has only one key:value pair, the enclosing curly brackets { } are not required:

.Example

[source,DataWeave,linenums]
---------------------------------------------------------
%dw 2.0
%output application/xml
---
name: "Annie"
---------------------------------------------------------

=== Conditional Elements

Objects can define conditional *key*: *value* pairs based on a conditional expression.
Wrap the key:value expression between parentheses and use the *when* keyword with the condition.

[source,DataWeave,linenums]
---------------------------------------------------------
%dw 2.0
%output application/xml
---
file: {
  name: "transform",
  (extension: "zip") when payload.fileSystem?
}
---------------------------------------------------------

This example outputs an additional field called "extension" only when the fileSystem property is present in payload (this field may contain any value, not just "true").

[source,xml,linenums]
--------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<file>
  <name>transform</name>
  <extension>zip</extension>
</file>
--------------------------------------------------------

If absent:

[source,xml,linenums]
--------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<file>
  <name>transform</name>
</file>
--------------------------------------------------------

=== Dynamic Elements

Dynamic elements allow you to add the result of an expression as key:value pairs of an object.

.Transform
[source,DataWeave,linenums]
--------------------------------------------------------
%dw 2.0
%output application/json
---
{
  a: "a",
  (["b","c","d"] map {'$': $})
}
--------------------------------------------------------

.Output
[source,json,linenums]
--------------------------------------------------------
{
  "a": "a",
  "b": "b",
  "c": "c",
  "d": "d"
}
--------------------------------------------------------
=== Dynamic keys

In order to specify a key via an expression, the expression should be wrapped in parentheses.

.Transform
[source,DataWeave,linenums]
----
%dw 2.0
%output application/json
---
{name: 'Data Weave'} mapObject {(upper($$ as String)) : $}
----

.Output
[source,json,linenums]
----
{
  "NAME": "Data Weave"
}
----

=== Conditional Attributes

Attributes can be conditional based on a given condition.
Wrap the key:value expression in parentheses and use the *when* keyword with the condition.

.Transform
[source,DataWeave,linenums]
----
%dw 2.0
%output application/xml
---
name @((company: "Acme") when false, (transform: "Anything") when true): "DataWeave"
----

.Output
[source,xml,linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<name transform="Anything">DataWeave</name>
----

=== Dynamic Attributes

Dynamic attributes allow you to add the result of an expression as key:value pairs of the attributes set.

.Input
[source,json,linenums]
----
{
  "company": "Mule",
  "product": "DataWeave"
}
----

.Transform
[source,dataweave,linenums]
----
%dw 2.0
%output application/xml
---
transformation @((payload)): "Transform from anything to anything"
----

.Output
[source,xml,linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<transformation company="Mule" product="DataWeave">Transform from anything to anything</transformation>
----


== String

*Type* => 'String'

A string can be defined by the use of double quotes or single quotes.

[source,DataWeave, linenums]
--------------------------------------------------------
{
  doubleQuoted: "Hello",
  singleQuoted: 'Hello',
}
--------------------------------------------------------

=== String interpolation

String interpolation allows you to embed variables or expressions directly in a string.

.Transform
[source,DataWeave, linenums]
--------------------------------------------------------
%dw 2.0
%output application/json
%var name = "Shoki"
---
{
    Greeting: "Hi, my name is $name",
    Sum: "1 + 1 = $(1 + 1)"
}
--------------------------------------------------------

.Output
[source,json,linenums]
--------------------------------------------------------
{
  "Greeting": "Hi, my name is Shoki",
  "Sum": "1 + 1 = 2"
}
--------------------------------------------------------

== Number

*Type* => 'Number'

There is only one number type that supports both floating point and integer numbers.
There is no loss of precision in any operation, the engine always stores the data in the most performant way that doesn't compromise precision.

== Boolean

*Type* => 'Boolean'

A boolean is defined by the keywords 'true' and 'false'.


== Dates

Dates in DataWeave follow the link:https://docs.oracle.com/javase/8/docs/api/java/time/format/DateTimeFormatter.html[ISO-8601 standard] and are defined between '|' characters.

The date system supports:

* DateTime
* Local DateTime
* Time
* Local Time
* Period
* TimeZone
* Date


=== Date

*Type* => 'Date'

Represented as 'Year'-'Month'-'Date'

The type *Date* has no time component at all (not even midnight).


.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
c: |2003-10-01|
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "c": "2003-10-01"
}
-----------------------------------------------------------------


=== Time

*Type* => 'Time'

Represented as 'Hour':'Minutes':'Seconds'.'Milliseconds'

.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
c: |23:59:56|
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "c": "23:59:56"
}
-----------------------------------------------------------------

=== TimeZone

*Type* => 'TimeZone'

Timezones must include a + or a - to be defined as such. |03:00| is a time, |+03:00| is a timezone.


.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
c: |-08:00|
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "c": "-08:00"
}
-----------------------------------------------------------------

=== DateTime

*Type* => 'Datetime'

Date time is the conjunction of 'Date' + 'Time' + 'TimeZone'.

.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
a: |2003-10-01T23:57:59-03:00|
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "a": "2003-10-01T23:57:59-03:00"
}
-----------------------------------------------------------------

=== Local Date Time

*Type* => 'Localdatetime'

Local date time is the conjunction of 'Date' + 'Time'.

.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
a: |2003-10-01T23:57:59|
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "a": "2003-10-01T23:57:59"
}
-----------------------------------------------------------------

=== Period

*Type* => 'Period'

Specifies a period of time. Examples |PT9M| => 9 minutes , |P1Y| => 1 Year

.Transform
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
%output application/json
---
a: |23:59:56| + |PT9M|
----------------------------------------------------------------

.Output
[source,json,linenums]
----------------------------------------------------------------
{
  "a": "00:08:56"
}
----------------------------------------------------------------


=== Date decomposition

In order to access the different parts of the date, special selectors must be used.

.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
%output application/json
---
{
  day: |2003-10-01T23:57:59Z|.day,
  month: |2003-10-01T23:57:59Z|.month,
  year: |2003-10-01T23:57:59Z|.year,
  hour: |2003-10-01T23:57:59Z|.hour,
  minutes: |2003-10-01T23:57:59Z|.minutes,
  seconds: |2003-10-01T23:57:59Z|.seconds,
  offsetSeconds: |2003-10-01T23:57:59-03:00|.offsetSeconds,
  nanoseconds: |23:57:59.700|.nanoseconds,
  milliseconds: |23:57:59.700|.milliseconds,
  dayOfWeek: |2003-10-01T23:57:59Z|.dayOfWeek,
  dayOfYear: |2003-10-01T23:57:59Z|.dayOfYear
}
-----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "day": 1,
  "month": 10,
  "year": 2003,
  "hour": 23,
  "minutes": 57,
  "seconds": 59,
  "offsetSeconds": -10800,
  "nanoseconds": 700000000,
  "milliseconds": 700,
  "dayOfWeek": 3,
  "dayOfYear": 274
}
-----------------------------------------------------------------

=== Changing the Format of a Date

You can specify a date to be in any format you prefer through using *as* in the following way:

.Transform
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
%output application/json
---
formatedDate: |2003-10-01T23:57:59| as String {format: "YYYY-MM-dd"}
----------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "formatedDate": "2003-10-01"
}
-----------------------------------------------------------------

If you are doing multiple similar conversions in your transform, you might want to define a custom type as a directive in the header and set each date as being of that type.

.Transform
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
%output application/json
%type Mydate = String { format: "YYYY/MM/dd" }
---
{
  formatedDate1: |2003-10-01T23:57:59| as Mydate,
  formatedDate2: |2015-07-06T08:53:15| as Mydate
}
----------------------------------------------------------------


.Output
[source,json,linenums]
-----------------------------------------------------------------
{
  "formatedDate1": "2003/10/01",
  "formatedDate2": "2015/07/06"
}
-----------------------------------------------------------------

[NOTE]
Type names are case-sensitive.

== Regular Expression

*Type* => 'Regex'

Regular Expressions are defined between /. For example /(\d+)/ for represents multiple numerical digits from 0-9.
These may be used as arguments in certain operations that act upon strings, like Matches or Replace, or on operations that act upon objects and arrays, such as filters.

== Iterators

*Type* => 'Iterator'

This type is based in the link:https://docs.oracle.com/javase/8/docs/api/java/util/Iterator.html[iterator Java class], that iterates through arrays. The iterator contains a collection, and includes methods to iterate through and filter it.

[NOTE]
Just like the Java class, the iterator is designed to be consumed only once. For example, if you then pass this value to a link:/mule-user-guide/v/4.0/logger-component-reference[logger] would result in consuming it and it would no longer be readable to further elements in the flow.



== Enum

This type is based in the link:https://docs.oracle.com/javase/7/docs/api/java/lang/Enum.html[Enum java class].
It must always be used with the `class` property, specifying the full java class name of the class, as shown in the example below.

.Transform
[source,DataWeave, linenums]
----
%dw 2.0
%output application/java
---
"Male" as Enum {class: "com.acme.GenderEnum"}
----

== CData


The link:/mule-user-guide/v/4.0/dataweave-types#xml[XML format] defines a custom type named CData, it extends from string and is used to identify a CDATA XML block.
It can be used to tell the writer to wrap the content inside CDATA or to check if the input string arrives inside a CDATA block. `CData` inherits from the type `String`.

.Transform
[source,DataWeave, linenums]
----------------------------------------------------------------------
%dw 2.0
%output application/xml
---
{
users:
{
user : "Mariano" as CData,
age : 31 as CData
}
}
----------------------------------------------------------------------

.Output
[source,xml,linenums]
----------------------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<users>
<user><![CDATA[Mariano]]></user>
<age><![CDATA[31]]></age>
</users>
----------------------------------------------------------------------




== Custom Types

You can define your own custom types in the header of your transform, then in the body you can define an element as being of that type.

To do so, the directive must be structured as following: `%type name = java definition`

For example:

[source,DataWeave,linenums]
----
%dw 2.0
%type Currency = Number { format: "##"}
%type User = Object { class: “my.company.User”}
----

[TIP]
====
Usually it's a good idea to extend an existing type rather than creating one from scratch.

For example, above `String` defines `Currency` as extending the String type.
====

To then assign an element as being of the custom type you defined, use the operation `as Type` after defining a field:

[source,DataWeave,linenums]
----
%dw 2.0
%type Currency = Number { format: "##"}
%type User = Object { class: “my.company.User”}
---
customer:payload.user as User
----


=== Defining Types For Type Coercion


==== Format

The metadata 'format' key is used for formatting numbers and dates.

.Input
[source,xml,linenums]
-----------------------------------------------------------------------
<items>
    <item>
        <price>22.30</price>
    </item>
    <item>
        <price>20.31</price>
    </item>
</items>
-----------------------------------------------------------------------

.Transform
[source,DataWeave, linenums]
-----------------------------------------------------------------------
%dw 2.0
%output application/json
%type Currency = Number { format: "##"}
---
books: payload.items.*item map
    book:
        price: $.price as Currency
-----------------------------------------------------------------------

.Output
[source,json,linenums]
-----------------------------------------------------------------------
{
  "books": [
    {
      "book": {
        "price": 22.30
      }
    },
    {
      "book": {
        "price": 20.31
      }
    }
  ]
}
-----------------------------------------------------------------------

In Anypoint Studio, you can define several more values, like separators, quote characters and escape characters. See link:/anypoint-studio/v/6/using-dataweave-in-studio#configuring-the-csv-reader[Using DataWeave in Studio].



== Functions and Lambdas

*type* -> Function

In DataWeave, function and lambdas (anonymous functions) are first-class citizen and they can be used inside operators such as a map, mapObject, etc, and can even be assigned to a variable.
When using lambdas within the body of a DataWeave file in conjunction with an operator such as link:/mule-user-guide/v/4.0/dataweave-operators#map[map operator], its attributes can either be explicitly named or left anonymous, in which case they can be referenced as $, $$, etc.



=== Assign to a var

You can define a function as a variable with a link:/mule-user-guide/v/4.0/dataweave-language-introduction#define-constant-directive[constant directive] through '%var'

.Transport
[source,DataWeave, linenums]
----
%dw 2.0
%output application/json
%var toUser = (user) -> {firstName: user.givenName, lastName: user.sn}
---
{
  "user" : toUser({ givenName : "Annie", sn : "Point" })
}
----

.Output
[source, json,linenums]
----
{
  "user": {
    "firstName": "Annie",
    "lastName": "Point"
  }
}
----

=== Named attributes with an Operator

This example uses a lambda with an attribute that's explicitly named as 'name'.

.Input
[source,DataWeave, linenums]
---------------------------------------------------------------------
%dw 2.0
%output application/json
---
users: ["john", "peter", "matt"] map ((name) ->  upper name)
---------------------------------------------------------------------

.Transform
[source,json,linenums]
---------------------------------------------------------------------
{
  "users": ["JOHN","PETER","MATT"]
}
---------------------------------------------------------------------

=== Anonymous attributes with an Operator

This example uses a lambda with an attribute that's not explicitly named, and so is referred to by default as '$'.

.Transform
[source,DataWeave, linenums]
---------------------------------------------------------------------
%dw 2.0
%output application/json
---
users: ["john", "peter", "matt"] map  upper $
---------------------------------------------------------------------

.Output
[source,json,linenums]
---------------------------------------------------------------------
{
  "users": ["JOHN","PETER","MATT"]
}
---------------------------------------------------------------------


=== Declare using function directive

You can declare functions in the Header and these can be invoked at any point in the Body, you can also declare functions anywhere in the body. You refer to them using the form *function-name()* passing an expression in between the parentheses for each necessary argument. Each expression between the parentheses is evaluated and the result is passed as an argument used in the execution of the function body.

.Transform
[source,DataWeave, linenums]
----
%dw 2.0
%output application/json
%function toUser(user){firstName: user.givenName, lastName: user.sn}
---
{
  "user" : toUser({ givenName : "Annie", sn : "Point" })
}
----

.Output
[source, json,linenums]
----
{
  "user": {
    "firstName": "Annie",
    "lastName": "Point"
  }
}
----




== Operators Sorted by Type

Below is an index that includes all of the different operators in DataWeave, sorted by the types of the parameters it accepts.
Each operator displays what type is accepted on each of its arguments, not all arguments are required.

[TIP]
When you provide an operator with properties that don't match the expected types, DataWeave automatically <<Type Coercion Table, attempts to coerce>> the provided property to the required type.

=== Operations Performed on any Type

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#typeOf[typeOf] |('Any')
| link:/mule-user-guide/v/4.0/dataweave-operators#as-(type-coercion)[as] |('Any', 'Type')
| link:/mule-user-guide/v/4.0/dataweave-operators#push[+] |('Any', 'Array')
|===


=== Operations Performed on 'Number'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#sum[+] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#minus[-] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#multiply[*] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#division[/] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#round[round] |('Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#sqrt[sqrt] |('Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#pow[pow] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#ceil[ceil] |('Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#floor[floor] |('Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#abs[abs] |('Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#mod[mod] |('Number', 'Number')
| link:/mule-user-guide/v/4.0/dataweave-operators#ordinalize[ordinalize] |('Number')
|===


=== Operations Performed on 'Array'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#min[min] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#max[max] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#sizeof[sizeOf] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#sum[sum] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#flatten[flatten] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#orderby[orderBy] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#reduce[reduce] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#array-push[+] |('Array', 'Any')
| link:/mule-user-guide/v/4.0/dataweave-operators#array-remove[-] |('Array', 'Any')
| link:/mule-user-guide/v/4.0/dataweave-operators#array-remove-all[--] |('Array', 'Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#map[map] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#avg[avg] |('Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#filter[filter] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#contains[contains] |('Array', 'Any')
| link:/mule-user-guide/v/4.0/dataweave-operators#distinctby[distinctBy] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#joinby[joinBy] |('Array', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#using-oncat-on-an-array[++] |('Array', 'Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#groupby[groupBy] |('Array', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#ziparrays[zip] |('Array', 'Array')
| link:/mule-user-guide/v/4.0/dataweave-operators#unziparray[unzip] |('Array')
|===

=== Operations Performed on 'String'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#trim[trim] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#using-concat-on-a-string[++] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#sizof[sizeOf] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#capitalize[capitalize] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#lower[lower] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#upper[upper] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#camelize[camelize] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#dasherize[dasherize] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#underscore[underscore] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#singularize[singularize] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#pluralize[pluralize] |('String')
| link:/mule-user-guide/v/4.0/dataweave-operators#split-by[splitBy ] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#split-by[splitBy] |('String', 'Regex')
| link:/mule-user-guide/v/4.0/dataweave-operators#find[find] |('String', 'Regex')
| link:/mule-user-guide/v/4.0/dataweave-operators#find[find] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#replace[replace] |('String', 'Regex', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#startswith[startsWith] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#endswith[endsWith] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#match[match] |('String', 'Regex')
| link:/mule-user-guide/v/4.0/dataweave-operators#matches[matches] |('String', 'Regex')
| link:/mule-user-guide/v/4.0/dataweave-operators#scan[scan] |('String', 'Regex')
| link:/mule-user-guide/v/4.0/dataweave-operators#contains[contains] |('String', 'String')
| link:/mule-user-guide/v/4.0/dataweave-operators#contains[contains] |('String', 'Regex')
|===

=== Operations Performed on 'Object'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#sizeof[sizeOf] |('Object')
| link:/mule-user-guide/v/4.0/dataweave-operators#orderby[orderBy] |('Object', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#using-map-on-an-object[map] |('Object', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#map-object[mapObject] |('Object', 'Function')
| link:/mule-user-guide/v/4.0/dataweave-operators#using-oncat-on-an-object[++] |('Object', 'Object')
| link:/mule-user-guide/v/4.0/dataweave-operators#using-remove-on-an-object[-] |('Object', 'Name')
| link:/mule-user-guide/v/4.0/dataweave-operators#remove-by-matching-key-and-value[--] |('Object', 'Object')
| link:/mule-user-guide/v/4.0/dataweave-operators#pluck[pluck] |('Object', 'Function')
|===



=== Operations Performed on 'Datetime'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#shift-time-zone[>>] |('Datetime', 'Timezone')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[+] |('Datetime', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-a-period-of-time[-] |('Datetime', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-two-dates[-] |('Datetime', 'Datetime')
|===

=== Operations Performed on 'Date'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-two-dates[-] |('Date', 'Date')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time[++] |('Date', 'Localtime')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time[++] |('Date', 'Time')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time-zone[++] |('Date', 'Timezone')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[+] |('Date', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[-] |('Date', 'Period')
|===

=== Operations Performed on 'Time'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-two-dates[-] | ('#Time', 'Time')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time[++] | ('#Time', 'Date')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[+] | ('Time', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[-] | ('Time', 'Period')
|===


=== Operations Performed on 'Localtime'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-two-dates[-] |('Localtime', 'Localtime')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time[++] |('Localtime', 'Date')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time-zone[++] |('Localtime', 'Timezone')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[+] |('Localtime', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[-] |('Localtime', 'Period')
|===

=== Operations Performed on 'Localdatetime'

[%header,cols="2*"]
|===
| Operator| Accepted types for each argument
| link:/mule-user-guide/v/4.0/dataweave-operators#subtracting-two-dates[-] |('Localdatetime', 'Localdatetime')
| link:/mule-user-guide/v/4.0/dataweave-operators#append-time-zone[++] |('Localdatetime', 'Timezone')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[+] |('Localdatetime', 'Period')
| link:/mule-user-guide/v/4.0/dataweave-operators#adding-a-period-of-time[-] |('Localdatetime', 'Period')
|===


== Type Coercion Table

In DataWeave, types can be coerced from one type to other using the link:/mule-user-guide/v/4.0/dataweave-operators#as[AS Operator].
This table shows the possible combinations and the properties from the schema that are used in the transformation.

[TIP]
When you <<DataWeave Operators Sorted by Type, provide an operator>> with properties that don't match the expected types, DataWeave automatically attempts to coerce the provided property to the required type.

|====
|Source                          |Target                  | Property
|Object                         |Array                  |   (1)
|Range                          |Array                  |
|Number                         |Binary                 |
|String                         |Binary                 |
|String                         |Boolean                |
|Number                         |Datetime               | unit
|Localdatetime                  |Datetime               |
|String                         |Datetime               | format / locale
|Datetime                       |Localdate              |
|Localdatetime                  |Localdate              |
|String                         |Localdate              | format / locale
|Datetime                       |Localdatetime          |
|String                         |Localdatetime          | format / locale
|Datetime                       |Localtime              |
|Localdatetime                  |Localtime              |
|Time                           |Localtime              |
|String                         |Localtime              | format / locale
|Datetime                       |Number                 | unit
|String                         |Number                 | format / locale
|Array                          |Object                 |
|String                         |Period                 |
|String                         |Regex                  |
|Datetime                       |String                 | format / locale
|Localdatetime                  |String                 | format / locale
|Localtime                      |String                 | format / locale
|Localdate                      |String                 | format / locale
|Timetype                       |String                 | format / locale
|Period                         |String                 |
|Timezone                       |String                 |
|Number                         |String                 | format / locale
|Boolean                        |String                 |
|Range                          |String                 | (2)
|Type                           |String                 |
|Datetime                       |Time                   |
|Localdatetime                  |Time                   |
|Localtime                      |Time                   |
|String                         |Time                   | format
|Datetime                       |Timezone               |
|Time                           |Timezone               |
|String                         |Timezone               |
|====


(1) Returns and array with all the values of the object.
(2) Returns a string with all the values of the range using "," as the separator

== See Also

* For a high level overview about the language, see link:/mule-user-guide/v/4.0/dataweave-language-introduction[DataWeave Language Introduction]
* For a reference about all of the operators that are available for using, see link:/mule-user-guide/v/4.0/dataweave-operators[DataWeave Operators]
* View complete example projects that use DataWeave in the link:https://www.mulesoft.com/exchange#!/?filters=DataWeave&sortBy=rank[Anypoint Exchange]
