= About Flow Control in DataWeave

You can use DataWeave control flow statements within the body of your DataWeave script.

[[control_flow_do]]
== `do`

A `do` statement creates a subroutine that is scoped by a variable. You specify it from the header of the DataWeave script. The statement surrounds its own DataWeave header and body in curly braces. The header uses `var` to initialize the variable, and the body can reference the variable.

This example uses `do` to return the string `"a"` when `myfun()` is called from the main body of the script.

.Transform
[source, dataweave, linenums]
----
%dw 2.0
output application/json
fun myfun() = do {
                    var a = "a"
                    ---
                    a
                 }
---
{ result: myfun() }
----

.Output
[source, json, linenums]
----
{
  "result": "a"
}
----

The next example uses `do` to prepend the string `"Foo"` to a string (`"Bar"`) that is passed to the `test(p: String)` function.

.Transform
[source, dataweave, linenums]
----
%dw 2.0
output application/json
fun test(p: String) = do {
                            var a = "Foo" ++ p
                            ---
                            a
                         }
---
{ result: test(" Bar") }
----

.Output
[source, json, linenums]
----
{
  "result": "Foo Bar"
}
----

[[control_flow_if_else]]
== `if else`

An `if` statement evaluates an expression enclosed in parenthesis as `true` or  `false` and conditionally executes a statement. You can make a single line conditional, or enclose a multi-line block of code in curly brackets.

.Transform
[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
if (payload.country == "USA")
  currency: "USD"
else currency: "EUR"
----

If the `if` expression evaluates to `false`, the corresponding part of the code is ignored, and the code that corresponds to the `else` expression is executed. Every `if` expression must have a matching `else` expression.

[[control_flow_else_if]]
== `else if`

You can chain several `else` expressions together within an if-else construct by incorporating `else if`, for example:

.Transform
[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
if (payload.country =="USA")
	currency: "USD"
else if (payload.country =="UK")
	currency: "GBP"
else currency: "EUR"
----

== See Also

* link:/mule-user-guide/v/4.0/dw-functions-core[DataWeave Core Functions]
* link:/mule-user-guide/v/4.0/dataweave-types[DataWeave Types]
* link:/mule-user-guide/v/4.0/dataweave-selectors[DataWeave Selectors]
